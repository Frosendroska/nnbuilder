buildscript {
  ext {
    protobufVersion = '3.19.1'
    grpcVersion = '1.42.1'
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.6.10'
  id 'com.google.protobuf'
  id 'com.moowork.node'
}

def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')

dependencies {
  implementation "io.grpc:grpc-protobuf:${grpcVersion}"
  implementation 'io.grpc:grpc-stub:1.40.1'
  implementation 'io.grpc:grpc-kotlin-stub:1.1.0'
  implementation "com.google.protobuf:protobuf-java:$protobufVersion"
  implementation "com.google.protobuf:protobuf-kotlin:$protobufVersion"
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1'

  compileOnly "jakarta.annotation:jakarta.annotation-api:1.3.5" // Java 9+ compatibility - Do NOT update to 2.0.0
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:${protobufVersion}"
  }

  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
    grpckt {
      artifact = 'io.grpc:protoc-gen-grpc-kotlin:1.2.1:jdk7@jar'
    }
    web {
      path = file('./node_modules/.bin/protoc-gen-grpc-web' + (isWindows ? '.cmd' : ''))
    }
  }

  generateProtoTasks {
    all().each {
      it.dependsOn npm_install
    }

    all()*.plugins {
      kotlin {}
      grpc {}
      grpckt {}
      js {
        option 'import_style=commonjs,binary'
      }
      web {
        option 'import_style=commonjs+dts,mode=grpcweb'
      }
    }
  }

  generatedFilesBaseDir = "$projectDir/src/generated"
  clean {
    delete generatedFilesBaseDir
  }
}

task copyJsToTs(type: Copy) {
  dependsOn tasks.withType(com.google.protobuf.gradle.GenerateProtoTask)
  from "$projectDir/src/generated/main/js"
  into "$projectDir/src/generated/main/web"
}

task index(type: NpmTask) {
  dependsOn copyJsToTs
  args = ['run', 'index']
}

task copyDts(type: NpmTask) {
  dependsOn npm_run_build
  args = ['run', 'copy-dts']
}

npm_run_build {
  dependsOn index
}

assemble.dependsOn npm_run_build, copyDts
tasks.withType(com.google.protobuf.gradle.GenerateProtoTask).all {
  dependsOn npm_install
}

idea {
  module {
    generatedSourceDirs += file('src/generated')
  }
}
