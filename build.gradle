plugins {
  id 'com.palantir.baseline' version '4.74.0'
  id 'org.inferred.processors' version '3.6.0'

  id 'java'
  id 'jacoco'
  id 'com.github.node-gradle.node' version '3.2.1'
  id 'com.diffplug.spotless' version '6.3.0'
  id 'com.palantir.docker-compose' version '0.32.0'

  id 'org.jetbrains.kotlin.jvm' version '1.6.10' apply false
  id 'com.google.protobuf' version '0.8.14' apply false
  id 'org.jetbrains.kotlin.plugin.spring' version '1.6.10' apply false
  id 'org.springframework.boot' version '2.6.4' apply false
  id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
}

allprojects {
  group = 'org.hse'
  version = '0.0.1-SNAPSHOT'

  apply plugin: 'java'
  apply plugin: 'com.diffplug.spotless'

  sourceCompatibility = '17'

  repositories {
    mavenCentral()
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
      freeCompilerArgs = ["-Xjsr305=strict"]
      jvmTarget = "17"
    }
  }

  node {
    version = '17.6.0'
    npmVersion = '8.5.1'
    download = true
    npmInstallCommand = project.hasProperty("isCI") ? "ci" : "install"
  }

  spotless {
    format 'misc', {
      target '*.gradle', '*.md', '.gitignore', '*.json', '*.yaml', '*.sh'
      targetExclude 'node_modules/*', 'package-lock.json'

      trimTrailingWhitespace()
      indentWithSpaces()
      endWithNewline()
    }
  }
}

task buildProxyConfig(type: Exec) {
  executable "scripts/dotenv.sh"
  args ".env", "envoy.template.yaml", "build/envoy.yaml"
}

task buildDockerConfig(type: Exec) {
  executable "scripts/dotenv.sh"
  args ".env", "docker-compose.template.yaml", "build/docker-compose.yaml"
}

dockerCompose {
  dockerComposeFile 'build/docker-compose.yaml'
}
dockerComposeUp.dependsOn buildProxyConfig, buildDockerConfig

task codeCoverageReport(type: JacocoReport) {
  executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

  subprojects.each {
    sourceSets it.sourceSets.main
  }

  reports {
    xml.enabled true
    xml.destination file("${buildDir}/reports/jacoco/report.xml")
    html.enabled false
    csv.enabled false
  }
}

codeCoverageReport.dependsOn {
  test
  subprojects*.test
}
